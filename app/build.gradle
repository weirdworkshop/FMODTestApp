apply plugin: 'com.android.application'

def fmod_api_root=project.file('../../../../../..').absolutePath

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.example.fmodtestapp"
        minSdk 30
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11", "-fno-exceptions", "-fno-rtti"
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField("String[]", "FMOD_LIBS", "{ \"fmodL\", \"fmodstudioL\" }")
            externalNativeBuild {
                cmake {
                    arguments '-DFMOD_API_ROOT=' + fmod_api_root,
                            '-DFMOD_LIB_SUFFIX=L'
                }
            }
        }
        release {
            buildConfigField("String[]", "FMOD_LIBS", "{ \"fmod\", \"fmodstudio\" }")
            externalNativeBuild {
                cmake {
                    arguments '-DFMOD_API_ROOT=' + fmod_api_root,
                            '-DFMOD_LIB_SUFFIX='
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ['res',fmod_api_root + '/studio/projects/media']
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation files(fmod_api_root + '/core/lib/fmod.jar')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}